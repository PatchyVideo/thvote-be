ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
int = { ("+" | "-")? ~ ASCII_DIGIT+ }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

int_inner = { int }
ident_inner = { ident }

value = {
    (int_inner | string | cp_value)
}

value_list = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ "]"
}

cp_value = {
    "(" ~ string ~ "," ~ string ~ ("," ~ string)* ~ ")"
}

in_condition = {
    ident ~ ":" ~ value_list
}

eq_condition = {
    ident ~ "=" ~ value
}

condition = {
    (in_condition | eq_condition)
}

or_query = {
	and_query ~ "OR" ~ query
}

query = {
    (or_query | and_query)
}

primary_query = {
    ("(" ~ query ~ ")" | condition)
}

and_query = {
    (primary_query ~ "AND" ~ and_query | primary_query)
}

root = {
    SOI
    ~ query
    ~ EOI
}

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
